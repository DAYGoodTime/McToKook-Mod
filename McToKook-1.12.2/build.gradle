buildscript {
    repositories {
        maven { url = 'https://maven.minecraftforge.net' }
        mavenCentral()
        gradlePluginPortal()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+',changing: true
        classpath 'gradle.plugin.com.github.johnrengelman:shadow:7.1.2'
    }
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'java'

version = '1.12.2-1.0'
group = 'com.xiaoACE'
archivesBaseName = 'McToKook'

java.toolchain.languageVersion = JavaLanguageVersion.of(8)

minecraft {
    mappings channel: 'stable', version: '39-1.12'
    runs {
        client {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            mods {
                mctokook {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            mods {
                mctokook {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            args '--mod', 'mctokook', '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')
            mods {
                mctokook {
                    source sourceSets.main
                }
            }
        }
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

repositories {
    mavenCentral()
    maven { url = 'https://jitpack.io' }
    maven { url 'https://maven.fabricmc.net/' }
    maven { url 'https://maven.architectury.dev/' }
}

configurations {
    shade
    implementation.extendsFrom shade
}

dependencies {
    minecraft 'net.minecraftforge:forge:1.12.2-14.23.5.2860'
    shade 'com.github.SNWCreations:Kookbc:0.26.13',{
        exclude group: 'net.fabricmc' ,module: 'sponge-mixin'
    }
    shade 'com.vdurmont:emoji-java:5.1.1'
    shade 'cn.hutool:hutool-all:5.8.18'
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

jar {
    archiveClassifier = 'slim'
    manifest {
        attributes([
                "Specification-Title"     : "mctokook",
                "Specification-Vendor"    : "xiaoACE",
                "Specification-Version"   : "1",
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : project.jar.archiveVersion,
                "Implementation-Vendor"   : "xiaoACE",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

shadowJar {
    archiveClassifier = ''
    configurations = [project.configurations.shade]
    exclude('META-INF/versions/')
    relocate('snw.kookbc','com.xiaoace.snw.kookbc')
    relocate('com.google.gson','com.xiaoace.com.google.gson')
    relocate('org.yaml.snakeyaml', 'com.xiaoace.org.yaml.snakeyaml')
    relocate('org.spongepowered', 'com.xiaoace.org.spongepowered')
    finalizedBy 'reobfShadowJar'
}

assemble.dependsOn shadowJar

reobf {
    shadowJar {}
}